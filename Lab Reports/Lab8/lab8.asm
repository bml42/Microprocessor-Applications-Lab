;lab 8 Benjamin Linam 4/4/18

REGBAS 	EQU	$1000	;BASE ADDRESS OF I/O REG BLOCK
PORTA	EQU	$00	;OFFSET OF PORTA
PORTC	EQU	$03	;OFFSET OF PORTC
DDRC	EQU	$07	;OFFSET OF DATA DIRECTION REG FOR PORT C
TOC2	EQU	$18	;OFFSET OF TOC2 FROM REGBAS
TCNT	EQU	$0E	;OFFSET OF TCNT FROM REGBAS
TCTL1	EQU	$20	;OFFSET OF TCTL1 FROM REGBAS
TFLG1	EQU	$23	;OFFSET OF TFLG1 FROM REGBAS
TOGGLE	EQU	$40	;VALUE TO SELECT TOGGLE ACTION OF PIN OC2
OC2	EQU	$40	;MASK TO SELECT OC2 PIN AND OC2F FLAG
CLEAR	EQU	$40	;VALUE TO CLEAR OC2F FLAG

	ORG 	$B600	
	LDX	#REGBAS		;X -> BASE PORT REGISTER
	
	LDAA 	#$F8		;SET PORT C AS INPUT 
	STAA	DDRC,X		;	""
	BSET	PORTA,X OC2	;SET OC2 PIN TO HIGH
	LDAA	#TOGGLE		;SELECT TOGGLE AS OUTPUT COMPARE ACTION
	STAA	TCTL1,X		;	""
	
	LDAB	PORTC,X		;RETRIEVES VALUE AT PORTC AND STORE IN [B]
	ANDB	#$07		;RETURNS 00000XXX
	LDY	#HITIME		;SET Y TO HITIME ARRAY		
	LSLB			;MULTIPLY [B] BY 2, ARRAY HAS 2-BYTE NUMBERS
	ABY			;ADD [B] TO Y, Y WILL POINT TO CORRECT HITIME VALUE

	LDD	TCNT,X		;START OC2 WITH HIGH TIME DELAY
	ADDD	0,Y		;	""
	STD 	TOC2,X		;	""
	LDAA	#CLEAR		;CLEAR OC2F FLAG
	STAA	TFLG1,X		;	""	

high	BRCLR	TFLG1,X OC2 high	;WAIT UNTIL OC2F FLAG SET TO 1
	
	LDAB	PORTC,X		;RETRIEVES VALUE AT PORTC AND STORE IN [B]
	ANDB	#$07		;RETURNS 00000XXX
	LDY	#LOTIME		;SET Y TO LOTIME ARRAY
	LSLB			;MULTIPLY [B] BY 2, ARRAY HAS 2-BYTE NUMBERS
	ABY			;ADD [B] TO Y, Y WILL POINT TO CORRECT LOTIME VALUE
	
	LDD	TOC2,X		;TOGGLE OC2 PIN AFTER LOW CYCLE
	ADDD	0,Y		;	""
	STD	TOC2,X		;	""
	LDAA	#CLEAR		;CLEAR THE OC2F FLAG
	STAA	TFLG1,X		;	""
low	BRCLR	TFLG1,X OC2 low		;WAIT UNTIL OC2F SET TO 1
	LDAA	#CLEAR			;CLEAR OC2F FLAG
	STAA	TFLG1,X			;	""

	LDAB	PORTC,X		;RETRIEVES VALUE AT PORTC AND STORE IN [B]
	ANDB	#$07		;RETURNS 00000XXX
	LDY	#HITIME		;SET Y TO HITIME ARRAY
	LSLB			;MULTIPLY [B] BY 2, ARRAY HAS 2-BYTE NUMBERS
	ABY			;ADD [B] TO Y, Y WILL POINT TO CORRECT HITIME VALUE
	
	LDD	TOC2,X		;START THE NEXT OC2 COMPARE OPERATION
	ADDD	0,Y		;WILL TOGGLE OC2 PIN HIGH
	STD	TOC2,X			;	""
	BRA	high

HITIME	FDB	200,600,400,500,600,350,400,450 	;HIGH TIME VALUES	
LOTIME	FDB	1800,1400,600,500,400,150,100,50	;LOW TIME VALUES
